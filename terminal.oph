; Copyright (C) 2017 David Boddie <david@boddie.org.uk>
;
; This program is free software: you can redistribute it and/or modify
; it under the terms of the GNU General Public License as published by
; the Free Software Foundation, either version 3 of the License, or
; (at your option) any later version.
;
; This program is distributed in the hope that it will be useful,
; but WITHOUT ANY WARRANTY; without even the implied warranty of
; MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
; GNU General Public License for more details.
;
; You should have received a copy of the GNU General Public License
; along with this program.  If not, see <http://www.gnu.org/licenses/>.

.org $1900

.alias key $70
.alias sent $71

.alias high_tone_bit $40
.alias tx_empty_bit $20
.alias rx_full_bit $10

main:

jsr setup_interrupt_routine

lda #$30        ; MODE 6, cassette input
sta $fe07

loop:

    lda #121
    ldx #0
    jsr $fff4

    cpx #255
    beq loop        ; X==255 -> loop again

    send_key:

    lda sent
    bne loop

    stx key
    lda #2
    sta sent
    
    lda #$34        ; MODE 6, cassette output
    sta $fe07

    jmp loop


;lda #$32        ; MODE 6, sound generation
;sta $fe07

rts

setup_interrupt_routine:

    sei         ; disable interrupts

    lda $204
    sta original_irq1v_low
    lda $205
    sta original_irq1v_high

    lda #<interrupt_routine
    sta $204
    lda #>interrupt_routine
    sta $205

    cli         ; enable interrupts

    rts

interrupt_routine:

    php
    pha
    txa
    pha

    lda $fe00
    and #tx_empty_bit
    beq interrupt_routine_exit

    ; TX Empty

    lda sent
    bne send_data

    lda #$30        ; MODE 6, cassette input
    sta $fe07
    bne interrupt_routine_exit  ; unconditional branch (lda flags)

    send_data:

    dec sent
    cmp #2                      ; value was 2
    beq interrupt_routine_exit

    ldx key
    stx $fe04

    interrupt_routine_exit:

    pla
    tax
    pla
    plp
    jmp (original_irq1v)

original_irq1v:
original_irq1v_low:  .byte 0
original_irq1v_high: .byte 0
